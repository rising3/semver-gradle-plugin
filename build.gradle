buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.github.rising3:semver-gradle-plugin:0.3.0"
    }
}

plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.gradle.plugin-publish" version "0.13.0"
    id "java"
    id "groovy"
    id "jacoco"
    id "com.github.hierynomus.license" version "0.15.0"
    id "com.diffplug.spotless" version "5.11.0"
    id "eclipse"
    id "idea"
//    id 'com.github.rising3.semver' version '0.3.0'
}

apply plugin: "com.github.rising3.semver"

group = "com.github.rising3"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven() { url = "https://maven.pkg.github.com/rising3/semver" }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation "org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r"
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
}

gradlePlugin {
    plugins {
        semver {
            id = "com.github.rising3.semver"
            implementationClass = "com.github.rising3.gradle.semver.plugins.SemVerGradlePlugin"
            displayName = "Gradle plugin for Updates the project version."
            description = "A plugin that can updating the semantic versions like `yarn version` command."
        }
    }
}

pluginBundle {
    website = "https://github.com/rising3/semver-gradle-plugin"
    vcsUrl = "https://github.com/rising3/semver-gradle-plugin.git"
    tags = ["semver", "semantic-versioning", "semantic", "version", 'release', "git", "yarn"]
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "semver-gradle-plugin",
                "Implementation-Version": project.version,
        )
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register("functionalTest", Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.named("check") {
    dependsOn(tasks.functionalTest)
}

test {
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
    }
}

spotless {
    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '*.md', '.gitignore'
        indentWithSpaces(2)
        endWithNewline()
    }
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat("1.8")
    }
    groovy {
        importOrder()
        // greclipse("4.17.0")
    }
}

license {
    header = rootProject.file("codequality/HEADER")
    strictCheck = true
    license.ext["year"] = Calendar.getInstance().get(Calendar.YEAR)
    license.ext["name"] = project.findProperty("author")
    license.ext["email"] = project.findProperty("email")
}

tasks.semver.dependsOn test

tasks.semver.configure {
    doFirst {
        println "doFirst preversion: $project.version"
    }
    doLast {
        println "doLast postversion: $project.version"
    }
}

task install {
    dependsOn publishToMavenLocal
}
